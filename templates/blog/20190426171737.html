<h2>【Perl】【Mojolicious】Webアプリを「デバッガ」で簡単にデバッグする方法</h2>

!<a href="https://qiita-image-store.s3.amazonaws.com/0/155418/54e08b7e-610c-7b77-fdc1-be2f0a076b62.jpeg">WIN_20170119_23_30_43_Pro.jpg</a>

Webアプリって目で見て確認するだけ? でも、本当に困ったときは、大事な部分は、プログラムの内部の動きも見てみたい。そんなときありますよね。

<h3> Mojoliciousのデバッグ</h3>

<strong>Mojolicious</strong>ではスクリプトを簡単に<strong>デバッグ</strong>することができます。とても短い記述で試験をかけるというのがうれしいですね。

まずはMojolicious::Liteで作成した簡単なWebアプリです。

<pre>
use Mojolicious::Lite;

get '/' => sub {
  my $self = shift;

  $DB::single = 1;    

  $self->render(text => 'Hello');
};

app->start;
</pre>

このファイルを「myapp.pl」という名前で保存してください。注目するポイントは<b>「$DB::single = 1」</b>という記述です。これはスクリプトの中にブレークポイントを埋め込むための記法です。デバッガはこの行を見つけるとその位置でストップします。

次に試験用のディレクトリを作成します。

<pre>
mkdir t
</pre>

tというディレクトリの中に試験用のスクリプトを配置します。このディレクトリの中に「basic.t」という名前のスクリプトを作成してみてください。

<h3> URLにHTTPアクセスするためのプログラム</h3>

では、試験スクリプトを書いてみましょう。

<pre>
use Test::More 'no_plan';

use strict;
use warnings;
use utf8;

use Test::Mojo;

use FindBin;
$ENV{MOJO_HOME} = "$FindBin::Bin/..";
require "$ENV{MOJO_HOME}/myapp.pl";

my $t = Test::Mojo->new;
$t->get_ok('/');
</pre>

このスクリプトはMojolicious::LiteのPODに記述されている自動試験用のスクリプトですが、デバッグを行うためにも利用することができます。

<b>「$t->get_ok('/')」</b>という記述で実際に「/」にアクセスした場合の処理が実行されるからです。

次にこのテストスクリプトををデバッガを使って起動しましょう。実行するディレクトリは、tディレクトリの中ではなくて、スクリプトのおいてあるディレクトリから行うのがよいでしょう。

<pre>
perl -d t/basic.t 
</pre>

libディレクトリの中にモジュールを配置してある場合は次のようにlibディレクトリをモジュールの検索パスに追加してあげます。

<pre>
perl -d -Ilib t/basic.t 
</pre>

<h3> ブレークポイントで止まるには</h3>

デバッガが起動したら「c」コマンドで「$DB::single = 1」と記述したブレークポイントまで進みます。

<pre>
c
</pre>

デバッグをしたい位置に到着することができます。

<pre>
5
6:          $DB::single = 1;
7
8==>        $self->render(text => 'Hello');
9:      };
10
11:     app->start;
</pre>

このテクニックを覚えておけば、Mojolicious::Liteの開発はいっそう楽なものになります。
